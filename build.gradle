plugins {
	id 'fabric-loom' version '0.8-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = "mc${project.minecraft_version}-${project.mod_version}"
if (project.build_number != "undefined") version = version + "+build.${project.build_number}"
group = project.maven_group

repositories {
	// modmenu maven 地址
	maven {
		url 'https://maven.terraformersmc.com/releases'
	}
	// rei maven 地址
	maven {
		url 'https://maven.shedaniel.me/'
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

	// Nullable修复
	modCompileOnly "com.google.code.findbugs:jsr305:3.0.2"
	// modmenu支持
	modCompileOnly "com.terraformersmc:modmenu:2.0.9"
	// rei支持
	modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:6.0.264-alpha"
}

minecraft {
	accessWidener "src/main/resources/cpp.accesswidener"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// 21w19a+ use java16
	it.options.release = 16

}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
//			groupId project.group
			artifactId project.archivesBaseName
//			version project.version
//			from components.java
//
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		maven {
			url 'https://maven.cjsah.net/repository/releases/'
			credentials {
				username project.maven_account
				password project.maven_password
			}
		}
	}
}
